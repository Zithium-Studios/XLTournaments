plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'net.zithium'
version = '3.17.12'
description = 'XLTournaments'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

repositories {
    mavenCentral()

    maven { url = uri("https://repo.papermc.io/repository/maven-public/") }
    maven { url = uri('https://oss.sonatype.org/content/groups/public') }
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://raw.githubusercontent.com/TeamVK/maven-repository/master/release/') }
    maven { url = uri('https://repo.codemc.io/repository/maven-public/') }
    maven { url = uri('https://mvn.lumine.io/repository/maven-public/') } // MythicMobs
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://repo.essentialsx.net/releases/') }
    maven { url = uri('https://libraries.minecraft.net/') }
}

dependencies {
    implementation('com.zaxxer:HikariCP:5.1.0')
    implementation('me.mattstudios.utils:matt-framework:1.4.6')

    implementation('dev.triumphteam:triumph-gui:3.1.11')

    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation('com.github.ItzSave:ZithiumLibrary:v2.1.1')

    // Adventure
    implementation('net.kyori:adventure-text-minimessage:4.17.0')
    implementation('net.kyori:adventure-text-serializer-legacy:4.17.0')

    // Apache
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation "io.papermc:paperlib:1.0.6"

    compileOnly 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    // EXTERNAL OBJECTIVES
    //compileOnly 'com.github.electro2560:ClueScrollsAPI:c5f0b7aa3a' // ClueScrolls
    compileOnly 'com.github.Realizedd.Duels:duels-api:3.5.1' // Duels
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1' // Vault
    //compileOnly 'com.vk2gpz.tokenenchant:TokenEnchantAPI:23.9.0' // TokenEnchant
    compileOnly 'me.clip:placeholderapi:2.11.5' // PlaceholderAPI
    compileOnly 'com.github.NuVotifier:NuVotifier:2.7.2' // nuVotifier
    compileOnly 'io.lumine:Mythic-Dist:5.6.1' // MythicMobs
    compileOnly 'net.essentialsx:EssentialsX:2.20.1' // EssentialsX
    compileOnly fileTree(dir: 'libs', include: '*.jar') // libs folder.
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    minimize()
    archiveFileName = "XLTournaments-${project.version}.jar"
    relocate 'dev.triumphteam.gui', 'net.zithium.tournaments.libs.gui' // GUI Library
    relocate 'me.mattstudios.mf', 'net.zithium.tournaments.libs.command' // Command Library
    relocate 'org.codemc.worldguardwrapper', 'net.zithium.tournaments.libs.worldguardwrapper' // Worldguard Wrapper
    relocate 'com.zaxxer.hikari', 'net.zithium.tournaments.libs.hikari' // SQLite (HikariCP)
    relocate 'org.bstats', 'net.zithium.tournaments.libs.metrics' // bStats
    relocate 'net.zithium.library', 'net.zithium.tournaments.libs.library' // ZithiumLibrary

    relocate "net.kyori.adventure", "net.zithium.tournaments.libs.adventure"
}
